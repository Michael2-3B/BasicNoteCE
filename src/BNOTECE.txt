ClrHome
SetUpEditor BNER
If dim(|LBNER
Then
	If |LBNER(1)=ln(23pi
	Then
		Output(1,1,"Ouch! Something crashed   BasicNote last time.
		Output(4,1,"Please PM me, Michael2_3B,@cemetech.net, or report  this crash at:
		Output(7,1,"cemete.ch/t14483
		Output(9,1,"Please leave as detailed areport as possible.
		Repeat getKey
		End
		pi->K
		ClrHome
	End
	If |LBNER(1)=ln(48pi
	Then
		Output(1,1,"It looks like you might   have a recurring error.   This is likely due to a   missing or tampered with  save file.
		Output(7,1,"Would you like to reset   all note data or continue?
		Output(10,1,"Reset
		Output(10,19,"Continue
		Repeat sum(Ans={11,15
			getKey
		End
		If Ans=11
		DelVar |LBMEMDelVar |LBNOTEDelVar |LBNS0DelVar |LBNS1DelVar |LBNS2DelVar |LBNS3DelVar |LBNS4DelVar |LBNS5DelVar |LBNS6DelVar |LBNS7DelVar |LBNS8
		ClrHome
	End
	If K!=pi
	ln(48pi->|LBNER(1
Else
	{ln(48pi),0,20->|LBNER
End
SetUpEditor BNOTE,BNS0,BNS1,BNS2,BNS3,BNS4,BNS5,BNS6,BNS7,BNS8
If dim(|LBNS0) and dim(|LBNOTE) and |LBNER(1)=ln(48pi
Then
	ClrHome
	Output(1,1,"Decompressing...
	Str8+"[xbar][ybar]->Str8
	Float
	8->L
	"|LBNS->Str2
	Str2+"8"->|u
	If dim(|LBNS0
	Then
		Repeat dim(|u
			If not(dim(|u
			Then
				L-1->L
				Str2+toString(Ans)->|u
			End
		End
	End
	" ->Str6
	For(A,0,L
		Str2+toString(A)->|u
		For(I,1,dim(|u
			|u
			Ans(I->N
			real(iPart(N
			If Ans
			Str6+sub(Str8,Ans,1->Str6
			real(|E3fPart(N
			If Ans
			Str6+sub(Str8,Ans,1->Str6
			imag(iPart(N
			If Ans
			Str6+sub(Str8,Ans,1->Str6
			imag(|E3fPart(N
			If Ans
			Str6+sub(Str8,Ans,1->Str6
		End
	End
	sub(Str8,1,length(Str8)-2->Str8
	sub(Str6,2,length(Str6)-1->Str6
	ClrHome
End
StoreGDB 0
BackgroundOff
CoordOff
GridOff
AxesOff
FnOff 
PlotsOff 
0->Xmin:264->Xmax
~164->Ymin:0->Ymax
Fix 3
Lbl A
ClrDraw
Horizontal ~15,Black,1
TextColor(Navy
Text(0,88,"BasicNote CE
TextColor(Black
Horizontal ~150,Black,1
Text(150,13,"New
Text(150,58,"Del All
TextColor(Black
Text(150,112,"About
Text(150,160,"Options
Text(150,225,"Exit
For(I,52.8,211.2,52.8
	Line(I,~164,I,~150,Black,1
End
SetUpEditor BNOTE,BMEM
6->dim(|LBMEM
If not(dim(|LBNOTE
Then
	"[ybar]->Str6
	{0->|LBNOTE
Else
	15->D:3->P
	For(I,1,11(|LBNOTE(1)>11)+|LBNOTE(1)(|LBNOTE(1)<=11
		inString(Str6,"[ybar]",P
		If inString(Str6,"[xbar]",P) and inString(Str6,"[xbar]",P)<Ans
		inString(Str6,"[xbar]",P
		If Ans-P<=0
		Ans+1
		Text(D,1,"    "+sub(Str6,P,Ans-P
		2+inString(Str6,"[ybar]",P->P
		D+12->D
	End
End
15->D:1->F:Ans->E:Ans->G
If |LBNOTE(1)>11
Then
	Line(264,~16,264,~149,0
	133/|LBNOTE(1
	Line(264,~16-(F-1)Ans,264,~149+(|LBNOTE(1)-F-10)Ans
End
"            "
For(I,1,4
	Ans+Ans
End
sub(Ans,1,187->Str3
ln(23pi->|LBNER(1
Repeat sum(K={1,15,45
	Text(D,1,">
	Repeat Ans
		getKey
	End
	Ans->K
	
	If K=14
	Then
		138
		If |LBNOTE(1)<11
		3+12|LBNOTE(1
		For(I,15,Ans,12
			Text(I,1,Str3
		End
		Lbl KO
		Float
		TextColor(Navy
		Text(0,70,"BasicNote Options
		TextColor(Red
		Text(15,1,">
		|LBNER(2
		Line(11,~23,13,~25,Ans
		Line(13,~25,19,~19,Ans
		If Ans
		Then
			TextColor(Black
			Text(27,24,"Save every
			Text(27,136,"edits.
			TextColor(Red
			Text(27,102,"<"+toString(|LBNER(3))+">
		End
		Line(10,~18,10,~28,Black,1
		Line(10,~18,20,~18,Black,1
		Line(10,~28,20,~28,Black,1
		Line(20,~18,20,~28,Black,1
		TextColor(Black
		Text(15,24,"Auto-Saving
		Repeat sum(K={45,11,12,13,14,15
			Repeat Ans
				getKey
			End
			Ans->K
			If Ans=21 or Ans=105
			Then
				not(|LBNER(2->|LBNER(2
				Line(11,~23,13,~25,Ans
				Line(13,~25,19,~19,Ans
				If Ans
				Then
					TextColor(Black
					Text(27,24,"Save every
					Text(27,136,"edits.
					TextColor(Red
					Text(27,102,"<"+toString(|LBNER(3))+">
				Else
					Text(27,1,Str3
					Line(10,~18,10,~28,Black,1
					Line(10,~28,20,~28,Black,1
					Line(20,~18,20,~28,Black,1
				End
			End
			If K=34 and |LBNER(2
			Then
				Text(15,1,"   
				TextColor(Red
				Text(27,1,">
				Repeat sum(K={21,105,45,25,11,12,13,14,15
					Text(27,102,"<"+toString(|LBNER(3))+">
					Repeat Ans
						getKey
					End
					Ans->K
					If Ans=24 and |LBNER(3)>10
					|LBNER(3)-10->|LBNER(3
					If K=26 and |LBNER(3)<90
					|LBNER(3)+10->|LBNER(3
				End
				Text(27,1,"   
				Text(15,1,">
				TextColor(Black
			End
		End
		Fix 3
		If K=13
		Then
			For(I,15,27,12
				Text(I,1,Str3
			End
		End
		If K=13
		Goto K3
		If not(sum(K={11,12,13
		1->K
	End

	If K=13
	Then
		138
		If |LBNOTE(1)<11
		3+12|LBNOTE(1
		For(I,15,Ans,12
			Text(I,1,Str3
		End
		Lbl K3
		TextColor(Navy
		Text(27,19,"Michael2_3B
		Text(0,70,"  About BasicNote  
		TextColor(Black
		Text(15,1,"BasicNote CE Version
		Text(27,1,"by
		Text(39,1,"Released
		TextColor(Red
		Text(15,148,"1.0.0
		Text(39,66,"07/26/2018
		TextColor(Blue
		Text(63,1,"Text Selection: [2nd] + [+]
		Text(75,1,"Copy Text: [sto-\>]
		Text(87,1,"Paste Text: [2nd] + [sto-\>]
		Text(99,1,"Special Characters: [2nd] + [0]
		TextColor(Black
		Text(123,1,"Forum Thread: cemete.ch/t14483
		Repeat sum(Ans={11,12,13,14,15,45
			getKey
		End
		Ans->K
		If K=14
		Then
			For(I,15,123,12
				Text(I,1,Str3
			End
		End
		If K=14
		Goto KO
		If not(sum(K={11,12
		1->K
	End
	
	If K=11
	Then
		1+|LBNOTE(1->E
		SetUpEditor CNOTE
		{1->|LCNOTE
		ClrDraw
		" ->Str7
		dim(|LCNOTE->O
		1->Q
		prgmZNEDITOR
		1->K
	End
	
	If K=12
	Then
		TextColor(Red
		Text(150,56,"Delete All Notes?
		TextColor(Blue
		Text(150,176,"<N>     
		0->I
		Repeat sum(K={21,45,105
			getKey->K
			If K=24 or K=26
			Then
				not(I->I
				If I
				Text(150,176,"<Y>
				If not(I
				Text(150,176,"<N>
			End
		End
		If I and sum(K={21,105
		Then
			DelVar |LBNOTE
			1->K
		Else
			DelVar K
			TextColor(Black
			Text(150,56,"                                                          "
			Text(150,112,"About
			Text(150,160,"Options
			For(I,52.8,211.2,52.8
				Line(I,~164,I,~150,Black,1
			End
			Text(150,58,"Del All
		End
		TextColor(Black
	End
	
	If K=23
	Then
		If |LBNOTE(1
		Then
			4:prgmZNOTECE
			1->K
		End
	End
	
	If sum(K={21,105
	Then
		If |LBNOTE(1
		Then
			For(I,1,E
				1+inString(Str6,"[ybar]",G->G
			End
			sub(Str6,G,inString(Str6,"[ybar]",G)-G->Str7
			1->Z:1->I
			If E>1
			Then
				1->J
				Repeat J=E
					I+1->I
					If |LBNOTE(I)=~1
					J+1->J
				End
			End
			I+1->I
			Ans->R
			0->dim(|LCNOTE
			While |LBNOTE(I)!=~1
				1+dim(|LCNOTE->dim(|LCNOTE
				|LBNOTE(I->|LCNOTE(dim(|LCNOTE
				I+1->I
			End
			dim(|LCNOTE->O
			TextColor(Black
			Line(264,~16,264,~149,0
			"                         "
			Text(150,56,Ans
			Text(150,160,Ans
			0->Q
			1:prgmZNOTECE
			prgmZNEDITOR
			1->K
		End
	End
	
	If sum(K={25,34
	Then
		If K=25
		Then
			Text(D,1,"   
			E-(E>1->E
			If F>1 and D=15
			Then
				F-1->F
				15->D:3->P
				For(I,1,F-1
					2+inString(Str6,"[ybar]",P->P
				End
				"            "
				For(I,1,4
					Ans+Ans
				End
				sub(Ans,1,187->Str2
				For(I,F,F+10
					inString(Str6,"[ybar]",P
					If inString(Str6,"[xbar]",P) and inString(Str6,"[xbar]",P)<Ans
					inString(Str6,"[xbar]",P
					Text(D,1,Str2
					Text(D,1,"    "+sub(Str6,P,Ans-P
					2+inString(Str6,"[ybar]",P->P
					D+12->D
				End
				15->D
				Line(264,~16,264,~149,0
				133/|LBNOTE(1
				Line(264,~16-(F-1)Ans,264,~149+(|LBNOTE(1)-F-10)Ans
			End
			D-12(D>15->D
		End
		If K=34
		Then
			Text(D,1,"   
			If |LBNOTE(1)>E
			Then
				If D<135
				Then
					E+1->E
					D+12->D
				Else
					F+1->F
					E+1->E
					15->D:3->P
					For(I,1,F-1
						2+inString(Str6,"[ybar]",P->P
					End
					"            "
					For(I,1,4
						Ans+Ans
					End
					sub(Ans,1,187->Str2
					For(I,F,F+10
						inString(Str6,"[ybar]",P
						If inString(Str6,"[xbar]",P) and inString(Str6,"[xbar]",P)<Ans
						inString(Str6,"[xbar]",P
						Text(D,1,Str2
						Text(D,1,"    "+sub(Str6,P,Ans-P
						2+inString(Str6,"[ybar]",P->P
						D+12->D
					End
					135->D
					Line(264,~16,264,~149,0
					133/|LBNOTE(1
					Line(264,~16-(F-1)Ans,264,~149+(|LBNOTE(1)-F-10)Ans
				End
			End
		End
	End
End
If K=1
Goto A
ClrDraw
ClrHome
Output(1,1,"Compressing...
DelVar |LBNS0DelVar |LBNS1DelVar |LBNS2DelVar |LBNS3DelVar |LBNS4DelVar |LBNS5DelVar |LBNS6DelVar |LBNS7DelVar |LBNS8
Fix 3
Str8+"[xbar][ybar]->Str8
For(I,0,length(Str6)/4
	1+int(I/999->X
	1+remainder(I,999->Y
	0
	If 4I+1<=length(Str6
	inString(Str8,sub(Str6,4I+1,1
	If 4I+2<=length(Str6
	Ans+inString(Str8,sub(Str6,4I+2,1))/|E3
	If 4I+3<=length(Str6
	Ans+inString(Str8,sub(Str6,4I+3,1))[i]
	If 4I+4<=length(Str6
	Ans+(inString(Str8,sub(Str6,4I+4,1))/|E3)[i]
	If X=1
	Ans->|LBNS0(Y
	If X=2
	Ans->|LBNS1(Y
	If X=3
	Ans->|LBNS2(Y
	If X=4
	Ans->|LBNS3(Y
	If X=5
	Ans->|LBNS4(Y
	If X=6
	Ans->|LBNS5(Y
	If X=7
	Ans->|LBNS6(Y
	If X=8
	Ans->|LBNS7(Y
	If X=9
	Ans->|LBNS8(Y
End
Archive |LBNS0
If X>1
Archive |LBNS1
If X>2
Archive |LBNS2
If X>3
Archive |LBNS3
If X>4
Archive |LBNS4
If X>5
Archive |LBNS5
If X>6
Archive |LBNS6
If X>7
Archive |LBNS7
If X>8
Archive |LBNS8
sub(Str8,1,length(Str8)-2->Str8
DelVar Str60->|LBNER(1
Float
RecallGDB 0
Archive |LBNOTE
ClrHome
