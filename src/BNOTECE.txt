DelVar KClrHome
"[|F][|F][|F][|F][|F]BPUW875668T888AK8888[|F]8888[|F]846888[|F][|F]888888888A98888889AA9A884[|F]248A88[|F][|F]888884896C8989878A8C98884[|F]248[|F][|F][|F][|F][|F][|F]afhCH8889QA8C6NGGG64GGG[|F][|F][|F]9N4CCAAA888AA84->Str9
"[|F][|F][|F][|F][|F]^^-1sin(cos(tan(^^^2,()/log(789*ln(456-[|F]123+[|F]0.~ABC[|F][|F]DEFGHIJKLMNOPQRSTUVWXYZtheta'[|F] :?abc[|F][|F]defghijklmnopqrstuvwxyztheta'[|F] :?[|F][|F][|F][|F][|F][|F]sin^-1(cos^-1(tan^-1(pisqrt(|E{}[e]10^(|u|v|w[e^(L4L5L6][|F]L1L2L3[|F][|F][|F][i]Ans!@#$%&=><>=<=_|"->Str8
SetUpEditor BNER,BNOTE,BMEM,BNS0,BNS1,BNS2,BNS3,BNS4,BNS5,BNS6,BNS7,BNS8
If dim(|LBNER
Then
	If |LBNER(1)=ln(23pi
	Then
		Output(1,1,"Ouch! Something crashed   BasicNote last time.
		Output(4,1,"Please PM me, Michael2_3B,@cemetech.net, or report  this crash at:
		Output(7,1,"cemete.ch/t14483
		Output(9,1,"Please leave as detailed areport as possible.
		Repeat getKey
		End
		pi->K
		ClrHome
	End
	If |LBNER(1)=ln(48pi) and dim(|LBNS0) and dim(|LBNOTE) and not(|LBNER(5
	Then
		Output(1,1,"Looks like your note file may be corrupted.
		Output(3,1,"Attempt decompression of  last save?
		Output(10,1,"Decompress
		Output(10,23,"Skip
		Repeat sum(Ans={11,15
			getKey
		End
		If Ans=11
		48pi->K
	End
	If |LBNER(1)=ln(48pi) and |LBNER(5
	Then
		Output(1,1,"It looks like you might   have a recurring error.   This is likely due to a   missing or tampered with  save file.
		Output(7,1,"Would you like to reset   all note data or continue?
		Output(10,1,"Reset
		Output(10,19,"Continue
		Repeat sum(Ans={11,15
			getKey
		End
		If Ans=11
		Then
			23pi->K
			DelVar |LBMEMDelVar |LBNOTEDelVar |LBNS0DelVar |LBNS1DelVar |LBNS2DelVar |LBNS3DelVar |LBNS4DelVar |LBNS5DelVar |LBNS6DelVar |LBNS7DelVar |LBNS8
		End
		ClrHome
	End
	If K!=pi
	ln(48pi->|LBNER(1
Else
	{ln(48pi),0,20,20,1->|LBNER
End
If K=23pi
Goto ST
If dim(|LBNS0) and dim(|LBNOTE) and |LBNER(1)=ln(48pi) and (|LBNER(5) or K=48pi
Then
	1->|LBNER(5
	ClrHome
	Output(1,1,"Decompressing...
	Str8+"[xbar][ybar]->Str8
	Float
	8->L
	"|LBNS->Str2
	Str2+"8"->|u
	If dim(|LBNS0
	Then
		Repeat dim(|u
			If not(dim(|u
			Then
				L-1->L
				Str2+toString(Ans)->|u
			End
		End
	End
	" ->Str6
	For(A,0,L
		Str2+toString(A)->|u
		For(I,1,dim(|u
			|u
			Ans(I->N
			real(iPart(N
			If Ans
			Str6+sub(Str8,Ans,1->Str6
			real(|E3fPart(N
			If Ans
			Str6+sub(Str8,Ans,1->Str6
			imag(iPart(N
			If Ans
			Str6+sub(Str8,Ans,1->Str6
			imag(|E3fPart(N
			If Ans
			Str6+sub(Str8,Ans,1->Str6
		End
	End
	sub(Str8,1,length(Str8)-2->Str8
	sub(Str6,2,length(Str6)-1->Str6
	ClrHome
	1->I
	~1->A
	Repeat I>length(Str6
		1+inString(Str6,"[ybar]",I->I
		A+1->A
	End
	A->|LBNOTE(1
	1->I
	While A
		Repeat |LBNOTE(I)=~1
			I+1->I
		End
		A-1->A
	End
	I->dim(|LBNOTE
End
Lbl ST
StoreGDB 0
BackgroundOff
CoordOff
GridOff
AxesOff
FnOff 
PlotsOff 
0->Xmin:264->Xmax
~164->Ymin:0->Ymax
Fix 3
Lbl A
ClrDraw
Horizontal ~15,Black,1
TextColor(Navy
Text(0,88,"BasicNote CE
TextColor(Black
Horizontal ~150,Black,1
Text(150,13,"New
Text(150,58,"Del All
TextColor(Black
Text(150,112,"About
Text(150,160,"Options
Text(150,225,"Exit
For(I,52.8,211.2,52.8
	Line(I,~164,I,~150,Black,1
End
SetUpEditor BNOTE,BMEM
6->dim(|LBMEM
If not(dim(|LBNOTE
Then
	"[ybar]->Str6
	{0->|LBNOTE
Else
	15->D:3->P
	For(I,1,11(|LBNOTE(1)>11)+|LBNOTE(1)(|LBNOTE(1)<=11
		inString(Str6,"[ybar]",P
		If inString(Str6,"[xbar]",P) and inString(Str6,"[xbar]",P)<Ans
		inString(Str6,"[xbar]",P
		If Ans-P<=0
		Ans+1
		Text(D,1,"    "+sub(Str6,P,Ans-P
		2+inString(Str6,"[ybar]",P->P
		D+12->D
	End
End
15->D:1->F:Ans->E:Ans->G
If |LBNOTE(1)>11
Then
	Line(264,~16,264,~149,0
	133/|LBNOTE(1
	Line(264,~16-(F-1)Ans,264,~149+(|LBNOTE(1)-F-10)Ans
End
"            "
For(I,1,4
	Ans+Ans
End
sub(Ans,1,187->Str3
ln(23pi->|LBNER(1
Repeat sum(K={1,15,45
	Text(D,1,">
	Repeat Ans
		getKey
	End
	Ans->K
	
	If K=14
	Then
		138
		If |LBNOTE(1)<11
		3+12|LBNOTE(1
		For(I,15,Ans,12
			Text(I,1,Str3
		End
		Lbl KO
		Float
		TextColor(Navy
		Text(0,70,"BasicNote Options
		|LBNER(2
		Line(11,~23,13,~25,Ans
		Line(13,~25,19,~19,Ans
		Line(10,~18,10,~28,Black,1
		Line(10,~18,20,~18,Black,1
		Line(10,~28,20,~28,Black,1
		Line(20,~18,20,~28,Black,1
		|LBNER(5
		Line(11,~35,13,~37,Ans
		Line(13,~37,19,~31,Ans
		Line(10,~30,10,~40,Black,1
		Line(10,~30,20,~30,Black,1
		Line(10,~40,20,~40,Black,1
		Line(20,~30,20,~40,Black,1
		TextColor(Black
		Text(15,24,"Auto-Saving
		Text(27,24,"Compression
		15->I
		Repeat sum(K={45,11,12,13,14,15
			TextColor(Red
			Text(I,1,">
			Repeat Ans
				getKey
			End
			Ans->K
			If sum(Ans={25,34
			Then
				Text(I,1,"    "
				15+12(K=34->I
			End
			If sum(K={21,105
			Then
				2+3(I=27
				not(|LBNER(Ans->|LBNER(Ans
				Line(11,~8-I,13,~10-I,Ans
				Line(13,~10-I,19,~4-I,Ans
			End
		End
		Fix 3
		If K=13
		Then
			For(I,15,27,12
				Text(I,1,Str3
			End
		End
		If K=13
		Goto K3
		If not(sum(K={11,12,13
		1->K
	End

	If K=13
	Then
		138
		If |LBNOTE(1)<11
		3+12|LBNOTE(1
		For(I,15,Ans,12
			Text(I,1,Str3
		End
		Lbl K3
		TextColor(Navy
		Text(27,19,"Michael2_3B
		Text(0,70,"  About BasicNote  
		TextColor(Black
		Text(15,1,"BasicNote CE Version
		Text(27,1,"by
		Text(39,1,"Released
		TextColor(Red
		Text(15,148,"1.1.2
		Text(39,66,"08/15/2018
		TextColor(Blue
		Text(63,1,"Text Selection: [2nd] + [+]
		Text(75,1,"Copy Text: [sto-\>]
		Text(87,1,"Paste Text: [2nd] + [sto-\>]
		Text(99,1,"Special Characters: [2nd] + [0]
		TextColor(Black
		Text(123,1,"Forum Thread: cemete.ch/t14483
		Repeat sum(Ans={11,12,13,14,15,45
			getKey
		End
		Ans->K
		If K=14
		Then
			For(I,15,123,12
				Text(I,1,Str3
			End
		End
		If K=14
		Goto KO
		If not(sum(K={11,12
		1->K
	End
	
	If K=11
	Then
		1+|LBNOTE(1->E
		SetUpEditor CNOTE
		{1->|LCNOTE
		ClrDraw
		" ->Str7
		dim(|LCNOTE->O
		1->Q
		prgmZNEDITOR
		1->K
	End
	
	If K=12
	Then
		TextColor(Red
		Text(150,56,"Delete All Notes?
		TextColor(Blue
		Text(150,176,"<N>     
		0->I
		Repeat sum(K={21,45,105
			getKey->K
			If K=24 or K=26
			Then
				not(I->I
				If I
				Text(150,176,"<Y>
				If not(I
				Text(150,176,"<N>
			End
		End
		If I and sum(K={21,105
		Then
			DelVar |LBNOTE
			1->K
		Else
			DelVar K
			TextColor(Black
			Text(150,56,"                                                          "
			Text(150,112,"About
			Text(150,160,"Options
			For(I,52.8,211.2,52.8
				Line(I,~164,I,~150,Black,1
			End
			Text(150,58,"Del All
		End
		TextColor(Black
	End
	
	If K=23
	Then
		If |LBNOTE(1
		Then
			4:prgmZNOTECE
			1->K
		End
	End
	
	If sum(K={21,105
	Then
		If |LBNOTE(1
		Then
			For(I,1,E
				1+inString(Str6,"[ybar]",G->G
			End
			sub(Str6,G,inString(Str6,"[ybar]",G)-G->Str7
			1->Z:1->I
			If E>1
			Then
				1->J
				Repeat J=E
					I+1->I
					If |LBNOTE(I)=~1
					J+1->J
				End
			End
			I+1->I
			Ans->R
			0->dim(|LCNOTE
			While |LBNOTE(I)!=~1
				1+dim(|LCNOTE->dim(|LCNOTE
				|LBNOTE(I->|LCNOTE(dim(|LCNOTE
				I+1->I
			End
			dim(|LCNOTE->O
			TextColor(Black
			Line(264,~16,264,~149,0
			"                         "
			Text(150,56,Ans
			Text(150,160,Ans
			0->Q
			1:prgmZNOTECE
			prgmZNEDITOR
			1->K
		End
	End
	
	If sum(K={25,34
	Then
		If K=25
		Then
			Text(D,1,"   
			E-(E>1->E
			If F>1 and D=15
			Then
				F-1->F
				15->D:3->P
				For(I,1,F-1
					2+inString(Str6,"[ybar]",P->P
				End
				"            "
				For(I,1,4
					Ans+Ans
				End
				sub(Ans,1,187->Str2
				For(I,F,F+10
					inString(Str6,"[ybar]",P
					If inString(Str6,"[xbar]",P) and inString(Str6,"[xbar]",P)<Ans
					inString(Str6,"[xbar]",P
					Text(D,1,Str2
					Text(D,1,"    "+sub(Str6,P,Ans-P
					2+inString(Str6,"[ybar]",P->P
					D+12->D
				End
				15->D
				Line(264,~16,264,~149,0
				133/|LBNOTE(1
				Line(264,~16-(F-1)Ans,264,~149+(|LBNOTE(1)-F-10)Ans
			End
			D-12(D>15->D
		End
		If K=34
		Then
			Text(D,1,"   
			If |LBNOTE(1)>E
			Then
				If D<135
				Then
					E+1->E
					D+12->D
				Else
					F+1->F
					E+1->E
					15->D:3->P
					For(I,1,F-1
						2+inString(Str6,"[ybar]",P->P
					End
					"            "
					For(I,1,4
						Ans+Ans
					End
					sub(Ans,1,187->Str2
					For(I,F,F+10
						inString(Str6,"[ybar]",P
						If inString(Str6,"[xbar]",P) and inString(Str6,"[xbar]",P)<Ans
						inString(Str6,"[xbar]",P
						Text(D,1,Str2
						Text(D,1,"    "+sub(Str6,P,Ans-P
						2+inString(Str6,"[ybar]",P->P
						D+12->D
					End
					135->D
					Line(264,~16,264,~149,0
					133/|LBNOTE(1
					Line(264,~16-(F-1)Ans,264,~149+(|LBNOTE(1)-F-10)Ans
				End
			End
		End
	End
End
If K=1
Goto A
ClrDraw
ClrHome
If |LBNER(5
Then
	Output(1,1,"Compressing...
	DelVar |LBNS0DelVar |LBNS1DelVar |LBNS2DelVar |LBNS3DelVar |LBNS4DelVar |LBNS5DelVar |LBNS6DelVar |LBNS7DelVar |LBNS8
	Fix 3
	Str8+"[xbar][ybar]->Str8
	For(I,0,length(Str6)/4
		1+int(I/999->X
		1+remainder(I,999->Y
		0
		If 4I+1<=length(Str6
		inString(Str8,sub(Str6,4I+1,1
		If 4I+2<=length(Str6
		Ans+inString(Str8,sub(Str6,4I+2,1))/|E3
		If 4I+3<=length(Str6
		Ans+inString(Str8,sub(Str6,4I+3,1))[i]
		If 4I+4<=length(Str6
		Ans+(inString(Str8,sub(Str6,4I+4,1))/|E3)[i]
		If X=1
		Ans->|LBNS0(Y
		If X=2
		Ans->|LBNS1(Y
		If X=3
		Ans->|LBNS2(Y
		If X=4
		Ans->|LBNS3(Y
		If X=5
		Ans->|LBNS4(Y
		If X=6
		Ans->|LBNS5(Y
		If X=7
		Ans->|LBNS6(Y
		If X=8
		Ans->|LBNS7(Y
		If X=9
		Ans->|LBNS8(Y
	End
	Archive |LBNS0
	If X>1
	Archive |LBNS1
	If X>2
	Archive |LBNS2
	If X>3
	Archive |LBNS3
	If X>4
	Archive |LBNS4
	If X>5
	Archive |LBNS5
	If X>6
	Archive |LBNS6
	If X>7
	Archive |LBNS7
	If X>8
	Archive |LBNS8
	sub(Str8,1,length(Str8)-2->Str8
	DelVar Str6
End
0->|LBNER(1
DelVar |LBMEM
Float
RecallGDB 0
Archive |LBNOTE
ClrHome