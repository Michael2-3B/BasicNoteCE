//1: Render note top to bottom
//1.1: Render note bottom to top
//1.2: Render note from current row to bottom
//2: calculate new cursor position and location
//3: save note
//4: delete note
//5: convert string to note, beginning to end
//5.1: convert string to note, current row to end
//5.2: backtrack if >262 and convert string to note from there

Ans->theta
If 1=iPart(Ans
Then
	"            "
	For(I,1,4
		Ans+Ans
	End
	sub(Ans,1,187->Str3
	15->H:2->C:dim(|LCNOTE
	If Ans-Z>10
	Then
		Z+10->D
	Else
		Ans->D
	End
	{Z,D,1
	If theta=1.1
	Then
		135->H
		Text(Ans,1,Str3
		{D,Z,~1
	End
	If theta=1.2
	Then
		abs(B+3->H
		Text(Ans,1,Str3
		{Z-1+abs(B/6+1)/2,D,1
	End
	For(I,Ans(1),Ans(2),Ans(3)
		If I>1
		1+int(|LCNOTE(I-1->C
		If C<=length(Str7) and fPart(|LCNOTE(I
		Then
			If "[xbar]"=sub(Str7,C,1
			C+1->C
			Text(H,1,Str3
			Text(H,1,sub(Str7,C,int(|LCNOTE(I)-(C-1)
		End
		If not(fPart(|LCNOTE(I
		Text(H,1,Str3
		H+12((theta=1 or theta=1.2)-(theta=1.1->H
	End
	If theta=1 or theta=1.2 and H<=135
	Then
		For(H,H,135,12
			Text(H,1,Str3
		End
	End
	Line(264,~16,264,~149,0
	If dim(|LCNOTE)>11
	Then
		133/dim(|LCNOTE
		Line(264,~16-(Z-1)Ans,264,~149+(dim(|LCNOTE)-Z-10)Ans
	End
Else
	If 2=theta
	Then
		Z-1+abs(B/6+1)/2->H
		If V>=|E3fPart(|LCNOTE(H
		Then
			|E3fPart(|LCNOTE(H->A
			iPart(|LCNOTE(H->P
		Else
			If H=1
			Then
				1->P
			Else
				iPart(|LCNOTE(H-1
				If inString(sub(Str7,Ans,iPart(|LCNOTE(H))-Ans),"[xbar]
				1+Ans
				Ans->P
			End
			If V
			Then
				0->N
				While N<V
					N->I
					If sub(Str7,P+1,1)!="[xbar]"
					N+inString("123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefgh",sub(Str9,inString(Str8,sub(Str7,P+1,1)),1->N
					P+1->P
				End
				If V-I<N-V
				Then
					I->N
					P-1->P
				End
				N->A
			Else
				0->A
			End
		End
	Else
		If 3=iPart(theta
		Then
			Text(150,5,"Saving
			If Q
			Then
				1+|LBNOTE(1->|LBNOTE(1
				Str6+Str7+"[ybar]->Str6
				augment(|LBNOTE,augment(|LCNOTE,{~1->|LBNOTE
				0->Q
				dim(|LCNOTE->O
			Else
				1->G
				For(I,1,E
					1+inString(Str6,"[ybar]",G->G
				End
				1->I
				If E>1
				Then
					1->J
					Repeat J=E
						I+1->I
						If |LBNOTE(I)=~1
						J+1->J
					End
				End
				I+1->I
				Ans->R
				dim(|LCNOTE->J
				If O<J
				Then
					J-O+dim(|LBNOTE->dim(|LBNOTE
					For(I,dim(|LBNOTE),R+J,~1
						|LBNOTE(I-(J-O))->|LBNOTE(I
						0->|LBNOTE(I-(J-O
					End
				Else
					If O>J
					augment(augment(seq(|LBNOTE(X),X,1,R-1),|LCNOTE),seq(|LBNOTE(X),X,R+O,dim(|LBNOTE->|LBNOTE
				End
				1->I
				While |LBNOTE(R)!=~1
					|LCNOTE(I->|LBNOTE(R
					I+1->I
					R+1->R
				End
				dim(|LCNOTE->O
				sub(Str6,1,G-1)+Str7+sub(Str6,inString(Str6,"[ybar]",G),length(Str6)-(~1+inString(Str6,"[ybar]",G->Str6
			End
			Text(150,4," Saved 
		Else
			If 4=theta
			Then
				1->I
				If E>1
				Then
					1->J
					Repeat J=E
						I+1->I
						If |LBNOTE(I)=~1
						J+1->J
					End
				End
				I+1->I
				seq(|LBNOTE(X),X,1,I-1->L1
				If E<|LBNOTE(1
				Then
					Repeat |LBNOTE(I)=~1
						I+1->I
					End
					augment(L1,seq(|LBNOTE(X),X,I+1,dim(|LBNOTE->L1
				End
				L1->|LBNOTE
				DelVar L1
				
				If 1=|LBNOTE(1
				Then
					"[ybar]->Str6
				Else
					1->G
					For(I,1,E
						1+inString(Str6,"[ybar]",G->G
					End
					sub(Str6,1,G-1
					If E<|LBNOTE(1
					Ans+sub(Str6,inString(Str6,"[ybar]",G)+1,length(Str6)-inString(Str6,"[ybar]",G
					Ans->Str6
				End
				|LBNOTE(1)-1->|LBNOTE(1
			Else
				If 5=iPart(theta
				Then
					"            "
					For(I,1,4
						Ans+Ans
					End
					sub(Ans,1,187->Str3
					DelVar GDelVar JDelVar L
					1->R:{Ans->L1:{0->L2
					If theta=5.1
					Then
						Z-1+abs(B/6+1)/2->R
						seq(iPart(|LCNOTE(X)),X,1,Ans->L1
						seq(|E3fPart(|LCNOTE(X)),X,1,R->L2
					End
					If theta=5.2
					Then
						Z-1+abs(B/6+1)/2->R
						iPart(|LCNOTE(Ans->X
						|E3fPart(|LCNOTE(R->I
						2
						If R>1
						1+iPart(|LCNOTE(R-1
						"I<=262->|v
						If inString(sub(Str7,Ans,X-Ans)," "
						"I<=262 and sub(Str7,X,1)=sub(Str8,66,1->|v
						Repeat |v or sub(Str7,X,1)="[xbar]
							I-inString("123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefgh",sub(Str9,inString(Str8,sub(Str7,X,1)),1->I
							X-1->X
						End
						Text(abs(B+3),I,Str3
						DelVar JDelVar L
						If sub(Str7,X,1)=" "
						Then
							X->J
							I->L
						End
						I->G
						X+I/|E3->|LCNOTE(R
						seq(iPart(|LCNOTE(X)),X,1,R->L1
						seq(|E3fPart(|LCNOTE(X)),X,1,R->L2
					End
					For(X,1+L1(dim(L1)),length(Str7
						If sub(Str7,X,1)="[xbar]
						Then
							If fPart(theta) and L1(dim(L1))<=262
							augment(L1,{X->L1
							1+dim(L2->dim(L2
							DelVar JDelVar LDelVar GR+1->R
						Else
							inString("123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefgh",sub(Str9,inString(Str8,sub(Str7,X,1)),1->W
							G+Ans->G
							If sub(Str7,X,1)=" "
							Then
								X->J
								G->L
							End
							If G>262
							Then
								R+1->dim(L1
								Ans->dim(L2
								If J
								Then
									J->L1(R
									L->L2(R
									R+1->R
									X->L1(Ans
									G-L->L2(R
									Ans->G
								Else
									X-1->L1(R
									G-W->L2(R
									R+1->R
									X->L1(Ans
									W->L2(R
									Ans->G
								End
							Else
								G->L2(R
								X->L1(R
							End
						End
					End
					For(I,1,dim(L1
						L1(I)+L2(I)/|E3->L1(I
					End
					L1->|LCNOTE
				End
			End
		End
	End
End
